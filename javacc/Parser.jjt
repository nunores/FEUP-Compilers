options {
    LOOKAHEAD = 1;
}

PARSER_BEGIN(Parser)

import java.util.ArrayList;
import java.util.HashMap;
import java.io.*;
import pt.up.fe.comp.jmm.report.*;

public class Parser {
    private ArrayList<Report> reports = new ArrayList<Report>();
    public ArrayList<Report> getReports() { return reports; }

    public static void main(String[] args) {}
}

PARSER_END(Parser)

SKIP : {
  " "
  | "\r"
  | "\t"
  | "\n"
}

// Keywords
TOKEN: {
    < TRUE: "true" >
	| < FALSE: "false" >
	| < THIS : "this" >
	| < NEW : "new" >
	| < INT: "int" >
	| < IMPORT: "import" >
	| < CLASS: "class" >
	| < EXTENDS: "extends" >
	| < IF: "if" >
	| < VOID: "void" >
	| < ELSE: "else" >
	| < LENGTH: "length" >
	| < MAIN: "main" >
	| < WHILE: "while" >
	| < PUBLIC: "public" >
	| < STATIC: "static" >
	| < RETURN: "return" >
	| < BOOLEAN: "boolean" >
}

// Separators
TOKEN: {
  < COL: "," >
  | < SEMICOL: ";" >
  | < DOT: "." >
  | < LBRACE: "{" >
  | < RBRACE: "}" >
  | < LPAR: "(" >
  | < RPAR: ")" >
  | < LBRACK: "[" >
  | < RBRACK: "]" >
}

// Operations
TOKEN: {
  < AND: "&&" >
  | < OR: "||" >
  | < LESS: "<" >
  | < PLUS: "+" >
  | < MINUS: "-" >
  | < MULT: "*" >
  | < DIV: "/" >
  | < EQ: "=" >
}

TOKEN : {
  < INTEGER_LITERAL: (["0"-"9"])+ >
  | < INTARRAY: "int[]" >
  | < STRINGARRAY: "String[]" >
  | < NOT: "!" >
  | < IDENTIFIER: ["A"-"Z","a"-"z","$","_"](["0"-"9","A"-"Z","a"-"z","_"])* >
}


JAVACODE
void error_skipto(int kind) {
  if (reports.size() == 10) { // "The compiler could report the first 10 errors found before aborting execution"
    System.out.println("10 errors found");
    return;
  }
  Token t;
  do {
    t = getNextToken();
  } while (t.kind != kind && t.kind != 0); // Treaing reaching EOF
    // The above loop consumes tokens all the way up to a token of
    // "kind".  We use a do-while loop rather than a while because the
    // current token is the one immediately before the erroneous token
    // (in our case the token immediately before what should have been
    // "if"/"while".
}


SimpleNode Program(): {} 
{
    try {
        ImportDeclaration() ClassDeclaration() <EOF>
    }
    catch (ParseException e) {
        Report r = new Report(ReportType.ERROR, Stage.SYNTATIC, token.beginLine, e.getMessage());
        reports.add(r);
        System.out.println("Program Exception");
    }
    { return jjtThis; }
}

void ImportDeclaration(): { Token t; String s = ""; }
{
    (
        <IMPORT> t=<IDENTIFIER> { s += t.image; }

        (<DOT> t=<IDENTIFIER> { s += "."+t.image; } )*
        <SEMICOL> { jjtThis.put("name", t.image); jjtThis.put("qualifiedName", s); }
    )*
}

void ClassDeclaration(): { Token t; }
{
    <CLASS> t=<IDENTIFIER> { jjtThis.put("className", t.image); } #Class
    [
        <EXTENDS> t=<IDENTIFIER> {jjtThis.put("extendedClass", t.image); } #Extends
    ]
    <LBRACE>
    (VarDeclaration())*
    (<PUBLIC> (MainDeclaration() | MethodDeclaration()))*
    <RBRACE>
}

void VarDeclaration(): { Token t; }
{
    Type() t=<IDENTIFIER> {jjtThis.put("name", t.image); } <SEMICOL>
}

void MethodDeclaration(): { Token t; }
{
    Type() t=<IDENTIFIER> { jjtThis.put("name", t.image); }
    <LPAR>
    [
        Parameter() (<COL> Parameter())*
    ]
    <RPAR>

    <LBRACE>
    (LOOKAHEAD(2) VarDeclaration())*
    (LOOKAHEAD(2) Statement())*
    ReturnExpression()
    <RBRACE>
}

void ReturnExpression(): {}
{
    <RETURN> Expression() <SEMICOL>
}

void Parameter(): { Token t; }
{
    Type() t=<IDENTIFIER> { jjtThis.put("name", t.image); }
}

void MainDeclaration(): { Token t; }
{
    <STATIC> <VOID> <MAIN>
    <LPAR>
    <STRINGARRAY>
    t=<IDENTIFIER> { jjtThis.put("name", t.image); } #Args
    <RPAR>

    <LBRACE>
    (LOOKAHEAD(2) VarDeclaration())* // VarDeclaration gives Type, which can be Identifier
    (LOOKAHEAD(2) Statement())*
    <RBRACE>
}


void Type(): { Token t; }
{
    t=<INTARRAY> { jjtThis.put("name", t.image); }
    | t=<BOOLEAN> { jjtThis.put("name", t.image); }
    | t=<INT> { jjtThis.put("name", t.image); }
    | t=<IDENTIFIER> { jjtThis.put("name", t.image); }
}

void Statement() #void: { Token t; }
{
    <IF> <LPAR> Expression() <RPAR> Statement()
    <ELSE> Statement()
    |
    try { <WHILE> <LPAR> Expression() <RPAR> }
    catch(ParseException e) {
      Report r = new Report(ReportType.ERROR, Stage.SYNTATIC, token.beginLine, e.getMessage());
      reports.add(r);
      error_skipto(RPAR);
    }
    Statement()
    |
    LOOKAHEAD(2) t=<IDENTIFIER> { jjtThis.put("name", t.image); } #Var <EQ> Expression() <SEMICOL> #Assignment(2)
    |
    LOOKAHEAD(2) t=<IDENTIFIER> { jjtThis.put("name", t.image); } #Var2 <LBRACK> Expression() <RBRACK> <EQ> Expression() <SEMICOL>
    |
    Expression() <SEMICOL>
    |
    <LBRACE>
    (Statement())*
    <RBRACE>
}

void Expression() #void: {}
{
    And()
}

void And() #void: {}
{
    Less() (LOOKAHEAD(2) <AND> Less() #And(2))*
}

void Less() #void: {}
{
    PlusMinus() (LOOKAHEAD(2) <LESS> PlusMinus() #Less(2))*
}

void PlusMinus() #void: { Token t; }
{
    MultDiv() (LOOKAHEAD(2) (t=<PLUS> { jjtThis.put("name", t.image); } MultDiv() | <MINUS> MultDiv()) #Operation(2))*
}

void MultDiv() #void: { Token t; }
{
    Rest() (LOOKAHEAD(2) (t=<MULT> { jjtThis.put("name", t.image); } Rest()| t=<DIV> { jjtThis.put("name", t.image); } Rest()) #Operation(2))*
}

void Rest(): { Token t;}
{
    t = <INTEGER_LITERAL> { jjtThis.put("name", t.image); } Terminal()
    |
    t = <TRUE> { jjtThis.put("name", t.image); } Terminal()
    |
    t = <FALSE> { jjtThis.put("name", t.image); } Terminal()
    |
    t = <IDENTIFIER> { jjtThis.put("name", t.image); } Terminal()
    |
    t = <THIS> { jjtThis.put("name", t.image); } Terminal()
    |
    t = <NEW> { jjtThis.put("new", t.image); }
    (
        (<INT> <LBRACK> Expression() <RBRACK> Terminal())
        |
        (t=<IDENTIFIER> { jjtThis.put("name", t.image); } <LPAR> <RPAR> Terminal())
    )
    |
    t = <NOT> { jjtThis.put("name", t.image); } Expression() Terminal()
    |
    <LPAR> Expression() <RPAR> Terminal()
}

void Terminal() #void: { Token t; }
{
    <LBRACK> Expression() <RBRACK> Terminal()
    |
    <DOT>
    (
        (
            t=<LENGTH> { jjtThis.put("name", t.image); } #DotLength Terminal()
        )
        |
        (
            t=<IDENTIFIER> { jjtThis.put("name", t.image); } #DotIdentifier <LPAR>
            [
                Expression()
                (<COL> Expression())*
            ]
            <RPAR>
            Terminal()
        )
    )
    |
    {}
}